set terminal svg font "Times New Roman, 20"
set terminal svg background "white"
set output "diffeq.svg"

set multiplot layout 2,1 title "{/:Bold Runge-Kutta method 12}"
set xlabel "x"
set ylabel "y"

set xzeroaxis
set yzeroaxis
set xtics ("0" 0, "0.5{/Symbol p}" pi/2, "{/Symbol p}" pi, \
		"1.5{/Symbol p}" 1.5*pi, "2{/Symbol p}" 2*pi, \
		"2.5{/Symbol p}" 2.5*pi, "3{/Symbol p}" 3*pi, \
		"3.5{/Symbol p}" 3.5*pi, "4{/Symbol p}" 4*pi)
set tics out
set grid
set key bottom right

ptype = 6
psize = 0.3

set title "Differential equation: u'' = u"
plot "diffeq1.txt" using 1:2 with points pt ptype ps psize lc rgb "red" title "Solution: u", \
"diffeq1.txt" using 1:3 with points pt ptype ps psize lc rgb "blue" title "Solution: u'"

set title "Differential equation:  {/Symbol q}'' + 0.25  {/Symbol q}' + 5 sin {/Symbol q} = 0"
set xtics 0, 2, 10
set ytics -4, 2, 4
set key bottom right
plot "diffeq2.txt" using 1:2 with points pt ptype ps psize lc rgb "red" title "Solution:  {/Symbol q}", \
"diffeq2.txt" using 1:3 with points pt ptype ps psize lc rgb "blue" title "Solution:  {/Symbol q}'"
